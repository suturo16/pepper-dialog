outputmacro: ^closeUser($_n)
              ^return(^join($CLOSEUSER ; $share_userid ; $_n ;  ))


topic: ~closings bot=pepper keep repeat (~emogoodbye  ~emohowzit ~BAD not ~GOOD ~emothanks ~emohappy  ~good_quality_adjectives ~yesanswer ~noanswer
                              ~bad_quality_adjectives ~emosad ~emoangry how meeting service talk discussion debate conversation speech later  
                               go away leave exit wish ~MISUNDERSTANDING
                               )


#! responders for MISUNDERSTANDING
u: ( ~MISUNDERSTANDING)
   $MISUNDERSTANDING


#! requesters for farewell

t: FAREWELL ()
   if($share_username!=null){$share_username} it was a pleasure to be with you

t: RESULTFAREWELL () how did you find our conversation and service
   a: ([<<not ~BAD>> << !not ~GOOD>>])
      [nice to hear it]
      [I am so proud of that]

   a: ([<<not ~GOOD>> << !not ~BAD>>])
     sorry. it will be better next time

t: FORFAREWELL ()
   [I am looking forward to see you again]
   [I hope to see you soon again]
   
   a: ([~yesanswer ([<<not ~BAD>> << !not ~GOOD>>]) ~emothanks ~emohappy  ~good_quality_adjectives ])
      ok

   a: ([~noanswer ([<<not ~GOOD>> << !not ~BAD>>]) ~emosad ~emoangry  ~bad_quality_adjectives ])
      sorry  

#! responders for farewell

u: (<<!how  [~emogoodbye later go away leave exit wish]>>) 
   are you [leaving][going]
   a: (~yesanswer)
	
	if($share_useramountcakeremaining>0){
          [but you still have $share_useramountcakeremaining piece of cake to take]
	  [your cake is not ready yet. you still have $share_useramountcakeremaining piece of cake to receive]
	  [you have not completed your order yet. you still have $share_useramountcakeremaining piece of cake to receive].
	  [do you want to drop the order]
	  [do you want to go anyway]
	  [do still want to leave]
        }else{
              ^closeUser(0)
	       [good bye]
	       [I am looking forward to see you again]
               [I hope to see you soon again]
        }
       if($share_username!=null){$share_username}
	b: (~yesanswer)
	   ^closeUser($share_useramountcakeremaining)
	   [good bye]
	   [I am looking forward to see you again]
           [I hope to see you soon again]
	b: (~noanswer)
	   [thank so much for your patience. you will get your cake very soon]
	   [I am so proud that you considered my suggestion]
	   [you are wellcome to our cafe. you get your cake delivered very soon]
	    
   a: (~noanswer) 
      sorry

u: ([ ~bad_quality_adjectives  ([<<not ~GOOD>> << !not ~BAD>>]) ~emosad ~emoangry])
   sorry. it will be better next time

u: ([ ~good_quality_adjectives  ([<<not ~BAD>> << !not ~GOOD>>]) ~emothanks ~emohappy])
   [nice to hear it]
   [I am so proud of that]

u: (~yesanswer)
   ok

u: (~noanswer)
   sorry

u: (<< how [meeting service talk discussion debate conversation speech]>>) 
   you mean how the conversation was
   a: (~yesanswer)
      I found it really interesting
   a: (~noanswer)
      [I am sorry I dont get you]
      [can you be clear a little bit more please]
      [could you please repeat]


